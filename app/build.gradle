import com.nikialeksey.interview.imagesearch.App
import com.nikialeksey.interview.imagesearch.Deps

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.dicedmelon.gradle.jacoco-android'

android {
    Properties properties = new Properties()
    def propsFile = project.rootProject.file('local.properties')
    if (propsFile.exists()) {
        properties.load(propsFile.newDataInputStream())
    }

    compileSdkVersion App.COMPILE_SDK_VERSION
    buildToolsVersion App.BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId App.APPLICATION_ID
        minSdkVersion App.MIN_SDK_VERSION
        targetSdkVersion App.TARGET_SDK_VERSION
        versionCode App.VERSION_CODE
        versionName App.VERSION_NAME
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            signingConfig signingConfigs.debug
            debuggable true

            buildConfigField "String", "FLICKR_API_KEY", "\"${properties.getProperty("flickr.api.key", "")}\""
            buildConfigField "String", "FLICKR_ENDPOINT", "\"https://www.flickr.com\""
        }
        instrumented {
            matchingFallbacks = ['debug']
            signingConfig signingConfigs.debug
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String", "FLICKR_API_KEY", "\"${properties.getProperty("flickr.api.key", "")}\""
            buildConfigField "String", "FLICKR_ENDPOINT", "\"https://www.flickr.com\""
        }
    }
    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }
}

dependencies {
    implementation project(':search:api')
    implementation project(':search:simple')

    implementation project(':show:api')
    implementation project(':show:simple')

    implementation project(':images:api')
    debugImplementation project(':images:flickr')
    releaseImplementation project(':images:flickr')
    instrumentedImplementation project(':images:fake')

    implementation Deps.KOTLIN_CORE
    implementation Deps.ANDROID_CORE
    implementation Deps.NAVIGATION

    implementation Deps.RETROFIT
    implementation Deps.OKHTTP

    implementation Deps.GLIDE
    kapt Deps.GLIDE_COMPILER

    testImplementation Deps.JUNIT
}
