import com.nikialeksey.interview.imagesearch.App
import com.nikialeksey.interview.imagesearch.Deps

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco-android'

android {
    Properties properties = new Properties()
    def propsFile = project.rootProject.file('local.properties')
    if (propsFile.exists()) {
        properties.load(propsFile.newDataInputStream())
    }

    compileSdkVersion App.COMPILE_SDK_VERSION
    buildToolsVersion App.BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId App.APPLICATION_ID
        minSdkVersion App.MIN_SDK_VERSION
        targetSdkVersion App.TARGET_SDK_VERSION
        versionCode App.VERSION_CODE
        versionName App.VERSION_NAME
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "FLICKR_API_KEY", "\"${properties.getProperty("flickr.api.key", "")}\""
        buildConfigField "String", "FLICKR_ENDPOINT", "\"https://www.flickr.com\""
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(':search')
    implementation project(':images:api')
    implementation project(':images:flickr')

    implementation Deps.KOTLIN_CORE
    implementation Deps.ANDROID_CORE
    implementation Deps.NAVIGATION
    implementation Deps.RETROFIT

    testImplementation Deps.JUNIT

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
